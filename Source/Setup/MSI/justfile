set unstable := true
set fallback := true
set dotenv-load := true
set windows-shell := ["powershell", "-c"]

# Dogfood ourselves

set script-interpreter := ["just", "gnu-tk", "-i", "-l", "/bin/sh", "-eu"]

# -----------------------------------------------------------------------------
# Recipes
# -----------------------------------------------------------------------------

# Show the help for this justfile
@help:
    just --list

# Start IDE using the project environment
[group("development")]
[windows]
develop:
    @Start-Process (Get-ChildItem -Path . -Filter *.slnx | Select-Object -First 1).FullName

# Start IDE using the project environment
[group("development")]
[unix]
develop:
    open *.slnx

build: _branding-build _project-build

clean: _project-clean _branding-clean

# -----------------------------------------------------------------------------

_project-build: _project-clean-release
    dotnet build -c Release /p:Platform=x64
    dotnet build -c Release /p:Platform=ARM64

_project-clean: _project-clean-release
    dotnet clean -c Debug -p:Platform=x64
    dotnet clean -c Debug -p:Platform=ARM64

_project-clean-release:
    dotnet clean -c Release -p:Platform=x64
    dotnet clean -c Release -p:Platform=ARM64

# -----------------------------------------------------------------------------

[script]
_branding-build:
    if [ -n "${GP_OSS_PROJECT_DEV_URL-}" ]; then
        if [ ! -d "Branding" ]; then
            mkdir Branding
            trap 'rm -rf Branding' ERR
            curl -fsSL "$GP_OSS_PROJECT_DEV_URL/msi-setup-branding.tar.gz" | tar -xz -C Branding
            trap - ERR
        fi
        echo "MSI setup branding applied."
    else
        just _branding-clean
    fi

[script]
_branding-clean:
    rm -rf Branding
